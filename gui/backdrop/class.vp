(include "sys/func.inc")
(include "gui/ctx/class.inc")
(include "gui/backdrop/class.inc")
(include "class/sym/class.inc")

(def-method 'backdrop :draw)
	;inputs
	;r0 = view object (ptr)
	;outputs
	;r0 = view object (ptr)
	;trashes
	;r1-r14

	(ptr 'this)
	(int 'x 'y 'w 'h)
	(uint 'col 'ink 'style)

	(push-scope)
	(entry 'backdrop :draw {this})

	(call 'backdrop :get_long_prop {this, static_sym_color} {_, col})
	(call 'backdrop :get_long_prop {this, static_sym_ink_color} {_, ink})
	(call 'backdrop :get_long_prop {this, static_sym_style} {_, style})
	(call 'backdrop :get_bounds {this} {_, _, _, w, h})

	(call 'ctx :set_color {this, col})
	(call 'ctx :filled_box {this, 0, 0, w, h})
	(call 'ctx :set_color {this, ink})
	(switch)
	(vpcase {style = 0})
		;grid
		(assign {((w >> 1) % 32) - 32, 0} {x, y})
		(loop-while {x < w})
			(call 'ctx :filled_box {this, x, y, 1, h})
			(assign {x + 32} {x})
		(loop-end)
		(assign {((h >> 1) % 32) - 32, 0} {y, x})
		(loop-while {y < h})
			(call 'ctx :filled_box {this, x, y, w, 1})
			(assign {y + 32} {y})
		(loop-end)
		(break)
	(vpcase {style = 1})
		;plain
		(break)
	(vpcase {style = 2})
		;axis
		(call 'ctx :filled_box {this, w >> 1, 0, 1, h})
		(call 'ctx :filled_box {this, 0, h >> 1, w, 1})
		(assign {((w >> 1) % 32) - 32, (h >> 1) - 16} {x, y})
		(loop-while {x < w})
			(call 'ctx :filled_box {this, x, y, 1, 32})
			(assign {x + 32} {x})
		(loop-end)
		(assign {((h >> 1) % 32) - 32, (w >> 1) - 16} {y, x})
		(loop-while {y < h})
			(call 'ctx :filled_box {this, x, y, 32, 1})
			(assign {y + 32} {y})
		(loop-end)
		(break)
	(vpcase {style = 3})
		;lines
		(assign {0} {y})
		(loop-while {y < h})
			(call 'ctx :filled_box {this, 0, y, w, 1})
			(assign {y + 32} {y})
		(loop-end)
		(break)
	(endswitch)

	(exit 'backdrop :draw {this})
	(pop-scope)
	(return)

(def-func-end)
