(include 'sys/func.inc)
(include 'sys/str/class.inc)
(include 'gui/font/class.inc)
(include 'gui/texture/class.inc)
(include 'class/sym/class.inc)
(include 'class/vector/class.inc)
(include 'class/hmap/class.inc)
(include 'class/pair/class.inc)
(include 'sys/pii/class.inc)
(include 'gui/gui/class.inc)
(include 'gui/points/class.inc)
(include 'gui/canvas/class.inc)
(include 'gui/ctx/class.inc)

(gen-create 'font)
(gen-vtable 'font)

(def-method 'font 'statics)
	;info
	;font static data
	(times font_statics_size (vp-byte 0))
(def-func-end)

(def-method 'font 'open)
	;r0 = name c string (pubyte)
	;r1 = font size (pixels)
	;outputs
	;r0 = 0 if error, else font object (ptr)
	;trashes
	;r0-r14

	(ptr 'this 'name 'flat_set 'data)
	(pptr 'iter_begin 'iter_end)
	(uint 'pixels)

	(push-scope)
	(entry 'font 'open {name, pixels})

	(assign (cat {@} (f-path 'font 'statics) {.font_statics_intern}) {flat_set})
	(vpifnot {flat_set})
		(call 'vector 'create nil {flat_set})
		(assign {flat_set} (cat {@} (f-path 'font 'statics) {.font_statics_intern}))
	(endif)

	;intern font
	(call 'vector 'get_both {flat_set} {_, iter_begin, iter_end})
	(assign {0} {data})
	(loop-while {iter_begin /= iter_end})
		(assign {*iter_begin} {this})
		(call 'sys_str 'compare {&this->font_name->str_data, name})
		(vpif `(,(method-output 'sys_str 'compare 0) = 0))
			(assign {this->font_data} {data})
			(gotoif {this->font_pixels = pixels} 'found)
		(endif)
		(assign {iter_begin + ptr_size} {iter_begin})
	(loop-end)
	(call 'font 'create {name, data, pixels} {this})
	(vpif {this})
		(call 'vector 'push_back {flat_set, this})
	(vp-label 'found)
		(call 'font 'ref {this})
	(endif)

	(exit 'font 'open {this})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'font 'init)
	;inputs
	;r0 = font object (ptr)
	;r1 = vtable (pptr)
	;r2 = name c string (pubyte)
	;r3 = 0, else ctf data string object (ptr)
	;r4 = font size (pixels)
	;outputs
	;r0 = font object (ptr)
	;r1 = 0 if error, else ok
	;trashes
	;r1-r14

	(ptr 'this 'name 'data)
	(union
		(ptr 'vtable)
		(ulong 'ok))
	(uint 'pixels)

	(push-scope)
	(entry 'font 'init {this, vtable, name, data, pixels})

	;init parent
	(s-call 'font 'init {this, vtable} {_, ok})
	(vpif {ok})
		;init state
		(assign {0, pixels} {this->font_chars, this->font_pixels})
		(call 'sym 'intern_cstr {name} {this->font_name})
		(call 'hmap 'create (cat {@} (f-path 'sym 'same) {, 61}) {this->font_word_map})
		(vpif {data})
			(call 'str 'ref {data} {this->font_data})
		(else)
			(call 'str 'create_from_file {name} {this->font_data})
			(vpifnot {this->font_data})
				(call 'str 'deref {this->font_name})
				(call 'hmap 'deref {this->font_word_map})
				(assign {0} {ok})
			(endif)
		(endif)
	(endif)

	(exit 'font 'init {this, ok})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'font 'deinit)
	;inputs
	;r0 = font object (ptr)
	;outputs
	;r0 = font object (ptr)
	;trashes
	;r1-r14

	(entry 'font 'deinit '(r0))

	(vp-push r0)
	(call 'vector 'deref_if '((r0 font_chars)))
	(assign '((rsp 0)) '(r0))
	(call 'hmap 'deref '((r0 font_word_map)))
	(assign '((rsp 0)) '(r0))
	(call 'str 'deref '((r0 font_name)))
	(assign '((rsp 0)) '(r0))
	(call 'str 'deref '((r0 font_data)))
	(vp-pop r0)

	;deinit parent
	(s-jump 'font 'deinit '(r0))

(def-func-end)

(def-method 'font 'get_metrics)
	;inputs
	;r0 = font object (ptr)
	;outputs
	;r0 = font object (ptr)
	;r1 = ascent (pixels)
	;r2 = descent (pixels)
	;r3 = height (pixels)
	;trashes
	;r1-r4

	(entry 'font 'get_metrics '(r0))

	(assign '((r0 font_data) (r0 font_pixels)) '(r3 r4))
	(vp-add-cr str_data r3)
	(assign '((r3 font_data_ascent) (r3 font_data_descent)) '(r1 r2))
	(vp-mul-rr r4 r1)
	(vp-mul-rr r4 r2)
	(assign '((& r1 r2)) '(r3))
	(vp-shr-cr 24 r1)
	(vp-shr-cr 24 r2)
	(vp-shr-cr 24 r3)

	(exit 'font 'get_metrics '(r0 r1 r2 r3))
	(vp-ret)

(def-func-end)

(def-method 'font 'glyph_data)
	;inputs
	;r0 = font object (ptr)
	;r1 = char code (uint)
	;outputs
	;r0 = font object (ptr)
	;r1 = 0, else glyph data pointer (ptr)
	;trashes
	;r1-r4

	(vp-def (this data c start end))

	(entry 'font 'glyph_data `(,this ,c))

	(assign `((,this font_data)) `(,data))
	(vp-add-cr (+ str_data font_data_size) data)
	(loop-start)
		(assign `((,data font_page_end)) `(,end))
		(breakif `(,end = 0))
		(assign `((,data font_page_start)) `(,start))
		(vp-add-cr font_page_size data)
		(vpif `(,c >= ,start) `(,c <= ,end))
			(vp-sub-rr start c)
			(vp-shl-cr (log2 int_size) c)
			(assign `((,data ,c ui)) `(,c))
			(assign `((,this font_data)) `(,data))
			(vp-add-cr str_data data)
			(vp-add-rr c data)

			(exit 'font 'glyph_data `(,this ,data))
			(vp-ret)
		(endif)
		(vp-sub-rr start end)
		(vp-add-cr 1 end)
		(vp-shl-cr (log2 int_size) end)
		(vp-add-rr end data)
	(endswitch)
	(vp-xor-rr data data)

	(exit 'font 'glyph_data `(,this ,data))
	(vp-ret)

(def-func-end)

(def-method 'font 'glyph_info)
	;inputs
	;r0 = font object (ptr)
	;r1 = utf8 encoded str object (ptr)
	;outputs
	;r0 = font object (ptr)
	;r1 = glyph info array object (ptr)
	;trashes
	;r1-r8

	(vp-def (this utf8 info data c) '(r6 r7 r8 r7 r1))

	(entry 'font 'glyph_info `(,this ,utf8))

	(call 'array 'create nil `(,info))
	(call 'array 'set_capacity `(,info (,utf8 str_length)) `(,info))
	(assign `((& ,utf8 str_data)) `(,data))
	(loop-start)
		(call 'sys_str 'read_utf8 `(,data) `(,data ,c))
		(breakif `(,c = 0))
		(call 'font 'glyph_data `(,this ,c) `(_ ,c))
		(call 'array 'push_back `(,info ,c))
	(loop-end)

	(exit 'font 'glyph_info `(,this ,info))
	(vp-ret)

(def-func-end)

(def-method 'font 'glyph_bounds)
	;inputs
	;r0 = font object (ptr)
	;r1 = utf8 encoded str object (ptr)
	;outputs
	;r0 = font object (ptr)
	;r1 = width (pixels)
	;r2 = height (pixels)
	;trashes
	;r1-r14

	(ptr 'this 'info)
	(pptr 'iter_begin 'iter_end)
	(uint 'w 'h 'gap)

	(push-scope)
	(entry 'font 'glyph_bounds {this, info})

	(assign {&this->font_data->str_data} {iter_begin})
	(assign {iter_begin->font_data_ascent + iter_begin->font_data_descent} {h})
	(assign {h >> 4} {gap})
	(assign {gap} {w})

	(call 'font 'glyph_info {this, info} {_, info})
	(call 'array 'get_both {info} {_, iter_begin, iter_end})
	(loop-while {iter_begin /= iter_end})
		(vpif {*iter_begin})
			(assign {w + (*iter_begin)->font_path_width + gap} {w})
		(endif)
		(assign {iter_begin + ptr_size} {iter_begin})
	(loop-end)
	(call 'array 'deref {info})

	(exit 'font 'glyph_bounds {this, (w * this->font_pixels >>> 24) + 1, h * this->font_pixels >>> 24})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'font 'glyph_paths)
	;inputs
	;r0 = font object (ptr)
	;r1 = stack array object (ptr)
	;r2 = utf8 encoded str object (ptr)
	;outputs
	;r0 = font object (ptr)
	;r1 = glyph paths vector object (ptr)
	;trashes
	;r1-r14

	(ptr 'this 'stack 'paths 'path 'info)
	(pptr 'iter_begin 'iter_end)
	(puint 'data 'data_end)
	(int 'x 'y 'pos_x 'pos_y 'width 'gap 'c 'ox 'oy)

	(push-scope)
	(entry 'font 'glyph_paths {this, stack, info})

	(assign {&this->font_data->str_data} {iter_begin})
	(assign {(iter_begin->font_data_ascent + iter_begin->font_data_descent) >> 4} {gap})
	(call 'font 'glyph_info {this, info} {_, info})
	(call 'array 'get_both {info} {_, iter_begin, iter_end})
	(call 'vector 'create nil {paths})
	(assign {gap, 0} {ox, oy})
	(loop-while {iter_begin /= iter_end})
		(assign {*iter_begin} {data})
		(vpif {data})
			(assign {data->font_path_width} {width})
			(assign {data->font_path_len} {data_end})
			(assign {((data + font_path_size) => data) + data_end} {data_end})
			(assign {ox, oy} {pos_x, pos_y})
			(loop-while {data /= data_end})
				(assign {data->font_path_element_type} {c})
				(assign {(data->font_line_element_x + ox) * this->font_pixels >>> 8} {x})
				(assign {(data->font_line_element_y + oy) * this->font_pixels >>> 8} {y})
				(switch)
				(vpcase {c = 0})
					;moveto
					(call 'points 'create nil {path})
					(call 'points 'push_back2 {path, x, y})
					(call 'vector 'push_back {paths, path})
					(assign {x, y, data + font_line_element_size} {pos_x, pos_y, data})
					(break)
				(vpcase {c = 1})
					;lineto
					(call 'points 'push_back2 {path, x, y})
					(assign {x, y, data + font_line_element_size} {pos_x, pos_y, data})
					(break)
				(vpcase {c = 2})
					;curveto
					(call 'points 'pop_back2 {path})
					(call 'points 'gen_cubic {path, stack, pos_x, pos_y, x, y,
						(data->font_curve_element_x1 + ox) * this->font_pixels >>> 8,
						(data->font_curve_element_y1 + oy) * this->font_pixels >>> 8,
						(data->font_curve_element_x2 + ox) * this->font_pixels >>> 8,
						(data->font_curve_element_y2 + oy) * this->font_pixels >>> 8,
						0.25})
					(assign {(data->font_curve_element_x2 + ox) * this->font_pixels >>> 8} {pos_x})
					(assign {(data->font_curve_element_y2 + oy) * this->font_pixels >>> 8} {pos_y})
					(assign {data + font_curve_element_size} {data})
				(endswitch)
			(loop-end)
			(assign {ox + width + gap} {ox})
		(endif)
		(assign {iter_begin + ptr_size} {iter_begin})
	(loop-end)
	(call 'array 'deref {info})

	(exit 'font 'glyph_paths {this, paths})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'font 'ref_word)
	;inputs
	;r0 = font object (ptr)
	;r1 = utf8 encoded str object (ptr)
	;outputs
	;r0 = font object (ptr)
	;r1 = 0, else texture object (ptr)
	;trashes
	;r1-r14

	(ptr 'this 'word 'txt 'word_canvas 'paths 'stack)
	(uint 'w 'h)
	(union
		(ptr 'font_statics)
		(pptr 'iter 'bucket))

	(push-scope)
	(entry 'font 'ref_word {this, word})

	;look up string in word map
	(call 'hmap 'find {this->font_word_map, word} {_, iter, bucket})
	(vpifnot {iter})
		;create word canvas
		(call 'array 'create nil {stack})
		(call 'font 'glyph_bounds {this, word} {_, w, h})
		(call 'canvas 'create {w, h, 1} {word_canvas})
		(assign {argb_white, 1} {word_canvas->canvas_color, word_canvas->canvas_flags})
		(call 'font 'glyph_paths {this, stack, word} {_, paths})
		(call 'canvas 'fill {word_canvas, 0})
		(call 'font 'get_metrics {this} {_, h, _, _})
		(call 'canvas 'fpoly {word_canvas, 0, h << fp_shift, 0, paths})
		(call 'canvas 'swap {word_canvas})

		;take texture from canvas
		(assign {word_canvas->canvas_texture} {txt})
		(assign {0} {word_canvas->canvas_texture})

		;free all the gubins
		(call 'canvas 'deref {word_canvas})
		(call 'array 'deref {stack})
		(call 'vector 'deref {paths})
		(breakifnot {txt})

		;create new entry
		(call 'sym 'ref {word})
		(call 'pair 'create {word, txt} {iter})
		(call 'vector 'push_back {bucket, iter})
		(call 'texture 'ref {txt})

		;flush cache ?
		(assign (cat {@} (f-path 'font 'statics)) {font_statics})
		(vpif {(font_statics->font_statics_count + 1 => font_statics->font_statics_count) >= font_max_word_cache})
			(call 'font 'flush)
		(endif)
	(else)
		(call 'pair 'ref_second {*iter} {_, txt})
	(endif)

	(exit 'font 'ref_word {this, txt})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'font 'ref_chars)
	;inputs
	;r0 = font object (ptr)
	;outputs
	;r0 = font object (ptr)
	;r1 = char texture vector object (ptr)
	;trashes
	;r1-r14

	(ptr 'this 'word 'txt 'chars)
	(uint 'code)

	;save inputs
	(push-scope)
	(entry 'font 'ref_chars {this})

	;lazy create char textures
	(vpifnot {this->font_chars})
		;note, don't write to member var yet in case flush gets called !
		(call 'vector 'create nil {chars})
		(call 'vector 'set_capacity {chars, 127 - 33})
		(assign {33} {code})
		(loop-start)
			(call 'sym 'intern_cstr {&code} {word})
			(call 'font 'ref_word {this, word} {_, txt})
			(vpif {txt})
				(call 'vector 'push_back {chars, txt})
			(endif)
			(call 'sym 'deref {word})
		(loop-until {(code + 1 => code) = 127})
		(vpif {this->font_chars})
			;another thread got to it during a flush !
			(call 'vector 'deref {chars})
		(else)
			(assign {chars} {this->font_chars})
		(endif)
	(endif)
	(call 'vector 'ref {this->font_chars})

	(exit 'font 'ref_chars {this, this->font_chars})
	(pop-scope)
	(return)

(def-func-end)

(def-method 'font 'flush)
	;trashes
	;r0-r14

	(ptr 'font 'new_map 'flat_set 'tmp)
	(uint 'length 'index)

	(push-scope)

	;must not use iters as kernel callbacks will deshedule us !
	(assign (cat {0, @} (f-path 'font 'statics) {.font_statics_intern}) {index, flat_set})
	(loop-start)
		(call 'vector 'get_length {flat_set} {_, length})
		(breakif {index = length})
		(call 'vector 'get_element {flat_set, index} {_, font})
		(vpif {font->font_chars})
			(vpif {font->font_chars->obj_count = 1})
				;during deref font_chars could be written to !
				(assign {font->font_chars} {tmp})
				(assign {0} {font->font_chars})
				(call 'vector 'deref {tmp})
			(endif)
		(endif)
		(call 'hmap 'create {font->font_word_map->hset_key_callback,
			font->font_word_map->hset_num_buckets} {new_map})
		(call 'hmap 'for_each {font->font_word_map, $flush_callback, &new_map})
		;during deref font_word_map could be written to !
		(assign {font->font_word_map} {tmp})
		(assign {new_map} {font->font_word_map})
		(call 'hmap 'deref {tmp})
		(assign {index + 1} {index})
	(loop-end)

	(pop-scope)
	(return)

(vp-label 'flush_callback)
	;inputs
	;r0 = predicate data (ptr)
	;r1 = element iterator (pptr)
	;outputs
	;r1 = 0 if break, else not
	;trashes
	;r1-r14

	(vp-cpy-ir r1 0 r1)
	(assign '((r1 pair_second)) '(r2))
	(assign '((r2 obj_count)) '(r2))
	(switch)
	(vpcase '(r2 /= 1))
		(vp-cpy-ir r0 0 r2)
		(call 'obj 'ref '(r1) '(r0))
		(vp-push r0)
		(call 'hset 'get_bucket '(r2 r0) '(_ r0))
		(vp-pop r1)
		(jump 'vector 'push_back '(r0 r1))
	(default)
		(f-bind 'font 'statics r0)
		(assign '((r0 font_statics_count)) '(r2))
		(vp-sub-cr 1 r2)
		(assign '(r2) '((r0 font_statics_count)))
	(endif)
	(vp-ret)

(def-func-end)
