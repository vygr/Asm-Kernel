(include "sys/func.inc")
(include "gui/path/class.inc")
(include "sys/math/class.inc")

(def-method 'path :transform)
	;inputs
	;r0 = path object (ptr)
	;r1 = source path object, can be same (ptr)
	;r2 = m1x (fixed)
	;r3 = m1y (fixed)
	;r4 = m2x (fixed)
	;r5 = m2y (fixed)
	;r6 = trx (fixed)
	;r7 = try (fixed)
	;outputs
	;r0 = path object (ptr)
	;trashes
	;r1-r14

	(ptr 'this 'that)
	(pptr 'iter_begin 'iter_end 'iter_this)
	(int 'm1x 'm1y 'm2x 'm2y 'trx 'try)

	(push-scope)
	(entry 'path :transform {this, that, m1x, m1y, m2x, m2y, trx, try})

	(call 'path :get_both {that} {_, iter_begin, iter_end})
	(call 'path :get_begin {this} {_, iter_this})

	(vec-set 0)
	(vec-load 3 {iter_end, iter_begin, iter_this})
	(bind '(e i j) (vec-top 3))
	(vec-load 2 {m1x, m1y})
	(defq m1 (vec-top 2))
	(vec-load 2 {m2x, m2y})
	(defq m2 (vec-top 2))
	(vec-load 2 {trx, try})
	(defq tr (vec-top 2))

	(loop-while `(,i /= ,e))
		(vec-load-long 2 i)
		(defq xy (vec-top 2))

		(vec-dup 2 m1)
		(vec-fdot 2 xy)
		(vec-dup 2 m2)
		(vec-fdot 2 xy)
		(vec-add 2 tr)
		(vec-store-long 2 j)
		(vp-add-cr (* 2 long_size) i)
		(vp-add-cr (* 2 long_size) j)
	(loop-end)

	(exit 'path :transform {this})
	(pop-scope)
	(return)

(def-func-end)
