(include 'sys/func.inc)
(include 'gui/canvas/class.inc)
(include 'gui/ctx/class.inc)
(include 'class/stream/class.inc)
(include 'class/array/class.inc)

(def-struct 'tga)
	(ubyte 'imagedata)	;id field offset
	(ubyte 'colmap)		;color map
	(ubyte 'type)		;targa type
	(ubyte 'a 'b)		;nint16 colmap_fei	;color map origin (non aligned !)
	(ubyte 'c 'd)		;nint16 colmap_num	;color map number (non aligned !)
	(ubyte 'colmap_size)	;color map size
	(struct 'pad 4)		;padding ?
	(ushort 'width)
	(ushort 'height)
	(ubyte 'depth)
	(ubyte 'origin)
(def-struct-end)

(def-method 'canvas 'load_tga)
	;inputs
	;r5 = stream object (ptr)
	;outputs
	;r0 = 0 if error, else canvas object (ptr)
	;trashes
	;all

	(ptr 'this 'stream)
	(puint 'data 'data_end)
	(ulong 'bit_pool)
	(struct 'header 'tga)
	(int 'length 'bit_pool_size)
	(ulong 'col)

	;save inputs
	(push-scope)
	(entry 'canvas 'load_tga {stream})

	;read header
	(call 'stream 'read {stream, &header, tga_size} {_, length})
	(gotoif {length /= tga_size || header.tga_type /= 2} 'error1)

	;create canvas
	(call 'canvas 'create {header.tga_width, header.tga_height, 1} {this})
	(vpif {this})
		(assign {32} {this->canvas_stream_type})
		(assign {this->canvas_array->array_begin} {data})
		(assign {data + this->canvas_height * this->canvas_stride} {data_end})
		(assign {0 => bit_pool} {bit_pool_size})

		;decode acording to type
		(loop-start)
			(call 'stream 'read_bits {stream, 32, bit_pool, bit_pool_size} {_, col, bit_pool, bit_pool_size})
			(gotoif {col = -1} 'error)
			(assign {col} {*data})
		(loop-until {(data + int_size => data) = data_end})
		(call 'canvas 'as_premul {this, this})
	(endif)

	(exit 'canvas 'load_tga {this})
	(return)

(vp-label 'error)
	(call 'canvas 'deref {this})
(vp-label 'error1)
	(exit 'canvas 'load_tga '(0))
	(pop-scope)
	(return)

(def-func-end)
